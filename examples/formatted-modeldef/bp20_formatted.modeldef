# Formatted BCMD translation of the BrainPiglet2.0 model
# J. Russell-Buckland
# joshua.russell-buckland.15@ucl.ac.uk
# 22/01/2018
# v2.0

# This model is identical to the brainpiglet2-1 model, but has been reformatted to improve readability and understanding.

## BrainPiglet v2.0
##
## BrainPiglet is a computational model of the piglet brain.
## @

@input SaO2sup P_a u Pa_CO2
# Return inputs to be plotted against
@output SaO2sup P_a u Pa_CO2
# NIRS measurements
@output CMRO2 Vmca TOI HHb HbO2 HbT

# Chemical reactions

# adenkin2
# aden1
2.0 [_ADP] <-> [_ATP] + [_AMP] {MA:k_ADPATP, 2.0} {MA:k_nADPATP}

# funcact
# a_red
4.0 [a] + (p1+10.0/6.0)/R_Hi [H] -> 2.0 [NAD] + (p1+4.0)/(R_Hic/Vol_mit) [_Hy] {f1}
# a_ox
p2/R_Hi [H] -> 4.0 [a] + 4.0 [bred] + p2/(R_Hic/Vol_mit) [_Hy] {f2}
# b_ox
[O2] + 4.0 [bred] + p3/R_Hi [H] -> {f3}
# psi_out
1.0/(R_Hic/Vol_mit) [_Hy] -> 1.0/R_Hi [H] {L}
# O2_in
-> 1.0/((1-df)*Vol_mit) [O2] {J_O2}

# glycol
# glycol
2.0 [_ADP] + 2.0 [_P] + [_gluc] + 2.0 [NADcyt] -> 2.0 [_ATP] + 2.0 [_Py0] + 4.0/R_Hic [_Hy]
	{MM : Vmax_glucN, Km_glucNA, Km_glucNP, Km_glucNgl, Km_glucNN}
# PytoLac
[_Py0] + 1.0/R_Hic [_Hy] <-> [_L0] + [NADcyt] {MA:k_PytoLac} {MA:k_nPytoLac}
# TCA
1.0/(1.0/Vol_mit) [_Py0] + 5.0 [NAD] + 1.0/(R_Hic/Vol_mit) [_Hy] -> 4.0/R_Hi [H] {k_TCA}
# glucose_in
-> [_gluc] {v_gluc_in*v_gluc_trans}
# glucose_out
[_gluc] -> {MM : v_gluc_trans, k_gluc_trans}
# lactate_in
-> [_L0] + 1.0/R_Hic [_Hy] {v_lac_in*v_lac_trans}
# lactate_out
[_L0] + 1.0/R_Hic [_Hy] ->
	{v_lac_trans*_L0*_Hy/((k_lac_trans + _L0)*(k_lac_h + _Hy))}
# MAshut
1.0/R_Hic [_Hy] + 1.0/Vol_mit [NAD] <-> [NADcyt] + 1.0/(R_Hi*Vol_mit) [H]
	{MA:k_MAshut} {MA:k_nMAshut}
# PCrtoATP
[_PCr] + [_ADP] + 1.0/R_Hic [_Hy] <-> [_ATP] + [_Cr] {MA:k_PCrATP} {MA:k_nPCrATP}
# ADPtoATP
[_ADP] + [_P] -> [_ATP] {kATP}
# ATPtoADP
[_ATP] -> [_ADP] + [_P] {MM : v_ATP, kmATP}


# Algorithmic relations

# XOvrel
XOv : 0 = CBF*(XOa-XOv)-J_O2
# O2crel
O2c : 0 = phi*pow(ScO2/(1.0-ScO2), 1.0/n_h)-O2c
# r_rel
r : 0 = T_e + T_m - (P_1-P_ic)*r

# Explicit differential equations

# v_prel
v_p' = 1.0/t_p*(P_a2 - v_p)
# v_urel
v_u' = 1.0/t_u*(u - v_u)
# v_crel
v_c' = 1.0/t_c*(Pa_CO2 - v_c)
# v_orel
v_o' = 1.0/t_o*(O2c - v_o)
# Dpsirel
Dpsi' = (p2*f2+p1*f1+p3*f3-L)/C_im


# Intermediates

## Arterio-venous volume ratio
## ~ dimensionless
## $AVR$
## + BloodFlow Oxygen
AVR = Vol_art/Vol_ven

## Cerebral Blood Flow
## ~ ml$_{blood}$ ml$_{brain}^{-1}$ s$^{-1}$
## $CBF$
## + BloodFlow Oxygen
CBF = G*(P_a2-P_v)

## Cytochrome c oxidase signal measured by NIRS.
## ~ $\mu$M
## $\Delta{}oxCCO$
## + NIRS Measurement
CCO = CCO_offset+1000.0*Vol_mit*((1-df)*a-a_n)

## Rate of cerebral oxygen metabolism.
## ~ mM s$^{-1}$
## $CMRO_{2}$
## + NIRS Measurement
CMRO2 = (1-df) * f3 * Vol_mit

## Natural buffering capacity of protons in mitochondria.
## ~ dimensionless
## $C_{0i,m}$
## + Metabolism
C_0i = ((pow(10.0,(-pH_m))) - (pow(10.0,(-pH_m-dpH))))/dpH

## Natural buffering capacity of protons in the cytoplasm.
## ~ dimensionless
## $C_{0i,c}$
## + Metabolism
C_0ic = ((pow(10.0,(-pH_o))) - (pow(10.0,(-pH_o-dpH))))/dpH

## Excess redox potential for NADH at normal demand.
## ~ mV
## $C_{NADH}$
## + Metabolism
C_NADH = Z/2.0*log10(1.0/NADNADHrat)

## Gibbs free energy of ATP hydrolysis
## ~ J mol$^{-1}$
## $\Delta{}G$
## + Metabolism
DGp = Gp0 + Z*Fdy*log10(gp)

## Change in total deoxygenated haemoglobin signal measured by NIRS
## ~ $\mu$M
## $\Delta{}HHb$
## + NIRS Measurement
DHHb = DHHb_offset + HHb-HHbn

## Change in total oxygenated haemoglobin signal measured by NIRS
## ~ $\mu$M
## $\Delta{}HbO_2$
## + NIRS Measurement
DHbO2 = DHbO2_offset + HbO2-HbO2n

## Change in total haemoglobin as measured by NIRS
## ~ $\mu$M
## $\Delta{}HbT$
## + NIRS Measurement
DHbT = DHbT_offset + HbT-HbTn

## Change in Hbdiff signal as measured by NIRS
## ~ $\mu$M
## $\Delta{}HbD$
## + NIRS Measurement
DHbdiff = DHbdiff_offset + (DHbO2 - DHHb)


## Proton motive force across the mitochondrial inner membrane.
## ~ mV
## $\Delta p$
## + Metabolism
Dp = Dpsi+ Z*DpH

## Difference between mitochondrial and extra-mitochondrial pH
## ~ dimensionless
## $\Delta pH$
## + Metabolism
DpH = pH_m-pH_o

## Value of $E_1$ when the reducing substrate is NADH.
## ~ mV
## $E_{1,NADH}$
## + Metabolism
E1NADH = (E_c0 - E_N0) + C_NADH

## The energy provided by electron transfer to Cu$_{A,r}$.
## ~ mV
## $E_{1}$
## + Metabolism
E_1 = E1NADH

## Effective conductance of the whole blood flow compartment.
## ~ ml$_{blood}$ ml$_{brain}^{-1}$ mmHg$^{-1}$ s$^{-1}$
## $G$
## + Autoregulation
G = K_G*pow(r, 4.0)

## Absolute deoxyhaemoglobin signal as measured by NIRS
## ~ $\mu$M
## $HHb$
## + NIRS Measurement
HHb = (Vol_art*Xa + Vol_ven*Xv)/4.0*blood_frac*1000.0

## Absolute oxyhaemoglobin signal as measured by NIRS
## ~ $\mu$M
## $HbO_2$
## + NIRS Measurement
HbO2 = (Vol_art*XOa + Vol_ven*XOv)/4.0*blood_frac*1000.0

## Absolute total haemoglobin signal as measured by NIRS
## ~ $\mu$M
## $HbT$
## + NIRS Measurement
HbT = (Vol_art+Vol_ven)*Xtot/4.0*blood_frac*1000.0

## Rate at which oxygen is supplied to the mitochondria, minimum of diffusion rate and delivery rate
## ~ $mM s^{-1}$
## $J_{O_{2},min}$
## + Oxygen
JO2 = fmin(D_O2*(O2c-O2), CBF*XOa)

## Rate at which oxygen is supplied to the mitochondria
## ~ $mM s^{-1}$
## $J_{O_{2}}$
## + Oxygen
J_O2 = JO2

## Equilibrium constant for the Cu$_A$ reduction reaction.
## ~ dimensionless
## $K_{eq_1}$
## + Metabolism
Keq1 = pow(10.0, (-1.0/Z*(p1*Dp/4.0 - E_1)))

## Equilibrium constant for the a$_3$ reduction reaction.
## ~ dimensionless
## $K_{eq_2}$
## + Metabolism
Keq2 = pow(10.0, (-1.0/Z*(p2*Dp/4.0 - E_2)))

## Rate of proton return to the mitochondrial matrix.
## ~ mM s$^{-1}$
## $L$
## + Metabolism
L = L_CV+L_lk

## Rate at which protons re-enter the mitochondrial matrix due to ADP phosphorylation.
## ~ mM s$^{-1}$
## $L_{CV}$
## + Metabolism
L_CV = CVinh*L_CVmax*(1.0 - exp(-theta))/(1.0 + r_CV*exp(-theta))

## Rate at which protons re-enter the mitochondrial matrix via leak channels.
## ~ mM s$^{-1}$
## $L_{lk}$
## + Metabolism
L_lk = k_unc*L_lk0*(exp(Dp*k_lk2)-1.0)

## Concentration of NADH in the mitochondria.
## ~ mM
## $NADH$
## + Metabolism
NADH = NADpool - NAD

## NAD/NADH ratio.
## ~ dimensionless
## $\frac{NAD}{NADH}$
## + Metabolism
NADNADHrat = NAD/NADH

## Average pressure in the blood vessels.
## ~ mm Hg
## $P_{1}$
## + Autoregulation
P_1 = (P_a + P_v) / 2.0
P_1 := P_1n

## Venous blood pressure.
## ~ mmHg
## $P_{v}$
## + Oxygen Link
P_v = ((Gt/G_v)*(P_a-P_vs)+P_vs)
P_v := P_vn

## Relative mitochondrial volume for protons
## ~ dimensionless
## $R_{Hi}$
## + Metabolism
R_Hi = r_buffi

## Relative cytoplasmic volume for protons
## ~ dimensionless
## $R_{Hi,c}$
## + Metabolism
R_Hic = r_buffic

## Capillary oxygen saturation.
## ~ dimensionless
## $S_{c,O_2}$
## + Oxygen
ScO2 = (SaO2sup + SvO2)/2.0

## Venous oxygen saturation.
## ~ dimensionless
## $S_{v,O_2}$
## + Oxygen
SvO2 = XOv/Xtot

## Total oxygenation index.
## ~ dimensionless
## $TOI$
## + NIRS Measurement
TOI = 100.0*((pow((r/r_n), 2.0)/(pow((r/r_n), 2.0) + (Vol_ven/Vol_artn)))*XOa+((Vol_ven/Vol_artn)/(pow((r/r_n), 2.0) + (Vol_ven/Vol_artn)))*XOv)/Xtot

## Elastic tension in the blood vessel walls.
## ~ mm Hg cm
## $T_{e}$
## + Autoregulation
T_e = sigma_e*h

## Muscular tension in the blood vessel walls.
## ~ mm Hg cm
## $T_{m}$
## + Autoregulation
T_m = T_max*exp(-pow(fabs((r-r_m)/(r_t-r_m)), n_m))

## Maximal muscular tension in the blood vessel walls.
## ~ mm Hg cm
## $T_{max}$
## + Autoregulation
T_max = T_max0*(1.0+k_aut*mu)

## Blood velocity in the middle cerebral artery.
## ~ cm s$^{-1}$
## $V_{mca}$
## + NIRS_blood
Vmca = CBF*CBFscale

## Relative arterial blood volume.
## ~ dimensionless
## $V_{a}$
## + NIRS
Vol_art = Vol_artn*pow((r/r_n), 2.0)

## Normalised total blood volume
## ~ dimensionless
## $V_{t}$
## + NIRS
Vol_tot = Vol_art + Vol_ven

## Venous blood volume as a fraction of total blood volume
## ~ dimensionless
## $V_{v}$
## + NIRS
Vol_ven = Vol_venn+C_v*(P_v-P_vn)

## Concentration of deoxyhaemoglobin in the veins
## ~ mM
## $[HHb_v]$
Xv = Xtot-XOv

## Concentration of reduced Cu$_A$.
## ~ mM
## $Cu_{A,r}$
## + Metabolism
ared = cytox_tot - a

## Concentration of oxidised cytochrome a$_3$.
## ~ mM
## $a_{3,o}$
## + Metabolism
b = cytox_tot - bred

## Merged autoregulation stimulus.
## ~ dimensionless
## $\eta$
## + Autoregulation
eta = R_autp*(v_p/v_pn - 1.0) + R_auto*(v_o/v_on-1.0) + R_autu*(1.0-v_u/v_un) + R_autc*(1.0 - v_c/v_cn)

## Reaction rate for the reduction of Cu$_A$.
## ~ mM s$^{-1}$
## $f_1$
## + Metabolism
f1 = k1*a-kn1*ared

## Reaction rate for the reduction of a$_3$.
## ~ mM s$^{-1}$
## $f_2$
## + Metabolism
f2 = k2*ared*b-kn2*a*bred

## Reaction rate for the reduction of O$_2$.
## ~ mM s$^{-1}$
## $f_3$
## + Metabolism
f3 = k3*O2*bred*exp(-c3*(Dp-Dp_30))/(1.0+exp(-c3*(Dp-Dp_30)))

## (Normalised) phophorylation potential (or ADP/ATP ratio)
## ~ dimensionless
## $g_{p}$
## + Metabolism
gp = (_ADP*_P)/(_ATP*1000.0)

## Thickness of the blood vessel walls.
## ~ cm
## $h$
## + Autoregulation
h = -r + sqrt(pow(r,2.0) + 2.0*r_0*h_0 + h_0*h_0)

## Forward reaction rate for the reduction of Cu$_A$.
## ~ s$^{-1}$
## $k_1$
## + Metabolism
k1 = k10*exp(-ck1*(Dp-Dp_n))

## Forward reaction rate for the reduction of Cu$_A$ at normal $\Delta p$.
## ~ s$^{-1}$
## $k_{1,0}$
## + Metabolism
k10 = k1_n*(NADpool/(1.0+NADNADHrat))/NADHn

## Reaction rate for the reduction of a$_3$.
## ~ mM s$^{-1}$
## $f_2$
## + Metabolism
k2 = k2_n*exp(-ck2*(Dp-Dp_n))

## Reverse reaction rate for the reduction of Cu$_A$.
## ~ s$^{-1}$
## $k_{-1}$
## + Metabolism
kn1 = k1/Keq1

## Reverse reaction rate for the reduction of a$_3$.
## ~ s$^{-1}$
## $k_{-2}$
## + Metabolism
kn2 = k2/Keq2

## Effective strength of the autoregulation reponse.
## ~ dimensionless
## $\mu$
## + Autoregulation
mu = (mu_min + mu_max*exp(eta))/(1.0 + exp(eta))

## Mitochondrial pH.
## ~ dimensionless
## $pH_{m}$
## + Metabolism
pH_m = -log10(H/1000.0)

## Extra-mitochondrial pH.
## ~ dimensionless
## $pH_{o}$
## + Metabolism
pH_o = -log10(_Hy/1000.0)

## Buffering capacity for protons in mitochondria.
## ~ dimensionless
## $r_{buffi,m}$
## + Metabolism
r_buffi = C_buffi/C_0i

## Buffering capacity for protons in cytoplasm.
## ~ dimensionless
## $r_{buffi,c}$
## + Metabolism
r_buffic = C_buffic/C_0ic

## Elastic stress in blood vessel walls.
## ~ mm Hg
## $\sigma_e$
## + Autoregulation
sigma_e = sigma_e0*(exp(K_sigma*(r-r_0)/r_0) - 1.0) - sigma_coll

## Driving force Complex V.
## ~ dimensionless
## $\theta$
## + Metabolism
theta = kCV*(Dp + Z/na*log10(gp/gp_n)-Dp_CV0)

## Total conductance of the cerebral blood vessel walls
## ~ ml$_{blood}$ ml$_{brain}^{-1}$ mmHg$^{-1}$ s$^{-1}$
## $G_t$
## + Autoregulation
Gt = ((G*G0*G_v)/(G*G0+G*G_v+G_v*G0))

## Pressure at the start of the cerebral artery compartment
## ~ mmHg
## $P_{a2}$
## + Autoregulation
P_a2 = ((G0*P_a+G*P_v)/(G+G0))

## Vmax for glycolysis
## ~ mMs$^{-1}$
## $v_{glyc}$
## + Metabolism
Vmax_glucN = Vmax_glucNn*(Inh_glucN + 1.0)/(1.0 + Inh_glucN*(_ATP/_ATPn)*(_AMPn/_AMP))

## Rate of the TCA cycle
## ~ mMs$^{-1}$
## $k_{TCA}$
## + Metabolism
k_TCA = v_TCA*_Py0*NAD/((Km_NAD + NAD)*(Km_Py+_Py0))

## Concentration of NADH in the cytoplasm
## ~ mM
## $[NADH_{cyt}]$
## + Metabolism
NADHcyt= (NADcytn + NADHcytn) - NADcyt

## Rate of forward reaction in the malate-aspartate shuttle
## ~ mM$^{-1}$s$^{-1}$
## $k_{MAshut}$
## + Metabolism
k_MAshut = ((CMRO2_n*NADHcyt)/3.0)/(NADn*_Hyn*NADHcyt - (1.0/Keq_MAshut)*NADcytn*H_n*NADH)

## Rate of backward reaction in the malate-aspartate shuttle
## ~ mM$^{-1}$s$^{-1}$
## $k^{-}_{MAshut}$
## + Metabolism
k_nMAshut = ((k_MAshut*NADH/NADHcyt)/Keq_MAshut)

## Rate of forward reaction in the pyruvate lactate equlibirum
## ~ mM$^{-1}$s$^{-1}$
## $k_{pl}$
## + Metabolism
k_PytoLac = ((2.0*CMRglucn-CMRO2_n/3.0)+k_nPytoLac*_L0n*NADcytn)/(_Py0n*_Hyn)*(NADHcyt/NADHcytn)

## Vmax of ATP use
## ~ mMs$^{-1}$
## $V_{max,ATP}$
## + Metabolism
v_ATP = (L_CVn*Vol_mit/na+2.0*CMRglucn)*(1.0+km)*u

## Rate of ATP synthesis by Complex V
## ~ mM$^{-1}$s$^{-1}$
## $k_{ATP,C_{V}}$
## + Metabolism
kATP = L_CV*Vol_mit/na

## Total exchangeable phosphate pool
## ~ mM
## $ EPP
## + Metabolism
EPP = _Pn + _PCrn + 2*_ATPn + _ADPn

## The ratio of [ATP]/EPP
## ~ dimensionless
## $NTP/EPP$
## + Metabolism
NTP_EPP = (1-df) * _ATP/EPP

## The ratio of [PCr]/EPP
## ~ dimensionless
## $PCR/EPP$
## + Metabolism
PCr_EPP = (1-df) * _PCr/EPP

## The ratio of [Pi]/EPP
## ~ dimensionless
## $P_{i}/EPP$
## + Metabolism
Pi_EPP = (1-df) * _P/EPP + df


# Initial values of root variables
_AMP := _AMPn
r := r_init
a := a_init
bred := bred_init
O2 := O2_init
XOv := XOv_init
O2c := O2c_init
H := H_init
_Hy := _Hyn
v_p := v_pn
v_u := v_un
v_c := v_cn
v_o := v_on
Dpsi := Dpsi_init
NAD := NADn
_Py0 := _Py0n
_L0 := _L0n
_gluc := _glucn
NADcyt := NADcytn
_PCr := _PCrn
_ADP := _ADPn
_ATP := _ATPn
_P := _Pn
_Cr := _Crn

# constraints
XOv >= 0
O2c >= 0
r >= 0
v_lac_trans > 0

# parameters

#parameters that may depend on input parameters
a_init = a_n
_AMPn = k_ADPATP_eq*pow(_ADPn, 2.0)/_ATPn
a_n = cytox_tot*a_frac_n
ared_n = cytox_tot - a_n
b_frac_n = 1.0 - bred_n/cytox_tot
b_init = b_n
b_n = cytox_tot - bred_n
bred_init = bred_n
bred_n = f_n/k3/O2_n/(exp(-c3*(Dp_n-Dp_30))/(1.0+exp(-c3*(Dp_n-Dp_30))))
C_NADH_n = Z/2.0*log10(1.0/NADNADHratn)
_Crn = (k_PCrATP_eq_ef*_ADPn*_PCrn)/_ATPn
cytox_tot = cytox_tot_tis/Vol_mit
DG1_n = -4.0*(E_1n+Z*log10(a_n/ared_n)) + p1*Dp_n
DG2_n = -4.0*(E_2+Z*(log10(ared_n/a_n)-log10(bred_n/b_n))) + p2*Dp_n
DGpn = Gp0 + Z*Fdy*log10(gp_n)
D_O2 = J_O2n/(O2c_n-O2_n)
Dp_30 = Dp_n + Dp3_corr
Dp_CV0 = -DGpn/(na*Fdy)
DpH_n = pH_mn - pH_on
Dp_init = Dp_n
Dp_n = Dpsi_n+Z*DpH_n
Dpsi_init = Dpsi_n
E1NADH_n = (E_c0 - E_N0) + C_NADH_n
E_1n = E1NADH_n
E_2 = (E_a30 - E_c0)
f_n = CMRO2_n/Vol_mit
G0 = K_G0*r0*r0*r0*r0
G0n = G0rat*Gn
glyc_an = pow(_ADPn,2.0)/(pow(Km_glucNA,2.0)+pow(_ADPn, 2.0))
glyc_gn = _glucn/(Km_glucNgl+_glucn)
glyc_nn = pow(NADcytn,2.0)/(pow(Km_glucNN,2.0)+pow(NADcytn, 2.0))
glyc_pn = pow(_Pn,2.0)/(pow(Km_glucNP,2.0)+pow(_Pn, 2.0))
Gn = CBFn/(P_an-P_vs)*(1.0+1.0/G0rat+1.0/G_VAratn)
gp_n = (_ADPn*_Pn)/(_ATPn*1000.0)
Gtn = Gn*G0n*G_v/(Gn*G0n+Gn*G_v+G_v*G0n)
G_v = G_VAratn*Gn
HbO2n = (Vol_artn*XOa_n + Vol_venn*XOv_n)/4.0*blood_frac*1000.0
HbTn = (Vol_artn+Vol_venn)*Xtot_n/4.0*blood_frac*1000.0
HHbn = (Vol_artn*Xa_n + Vol_venn*Xv_n)/4.0*blood_frac*1000.0
H_init = H_n
H_n = pow(10.0, 3.0-pH_mn)
h_n = -r_n + sqrt(pow(r_n,2.0) + 2.0*r_0*h_0 + h_0*h_0)
H_on = pow(10.0, 3.0-pH_on)
_Hyn = 1000.0*pow(10.0,-pH_on)
J_O2n = CMRO2_n
k1_n = f_n/(a_n-1.0/Keq1_n*ared_n)
k2_n = f_n/(ared_n*b_n-1.0/Keq2_n*a_n*bred_n)
k3 = k30/(exp(-c3*(Dp_n-Dp_30))/(1.0+exp(-c3*(Dp_n-Dp_30))))
kADP2 = L_CVn/na
k_ADPATP_eq = k_ADPATP/k_nADPATP
kCV = -1.0/(Dp_n-Dp_CV0)*log((1.0-L_CV0)/(1.0+r_CV*L_CV0))
Keq1_n = pow(10.0, (-1.0/Z*(p1*Dp_n/4.0 - E_1n)))
Keq2_n = pow(10.0, (-1.0/Z*(p2*Dp_n/4.0 - E_2)))
K_G0 = G0n
K_G = Gn/pow(r_n, 4.0)
kmATP = _ATPn*km
Km_glucNA = Km_glucNAf*_ADPn
Km_glucNP = Km_glucNPf*_Pn
Km_NAD = Km_NADf*NADn
Km_Py = Km_Pyf*_Py0n
kn1_n = f_n/(Keq1_n*a_n-ared_n)
kn2_n = f_n/(Keq2_n*ared_n*b_n-a_n*bred_n)
k_nPCrATP = log(2.0)/((k_PCrATP_eq*(_PCrn+_ADPn+(_Hyn/(R_Hic)))+_Crn+_ATPn)*k_PCrATP_ht)
k_nPytoLac = k_PytoLac_ht
k_PCrATP_eq = k_PCrATP_eq_ef/_Hyn
k_PCrATP = log(2.0)/((_PCrn+_ADPn+(_Hyn/(R_Hic))+(_Crn+_ATPn)/k_PCrATP_eq)*k_PCrATP_ht)
k_TCAn = (1.00/3.00)*(CMRO2_n/Vol_mit)
L_CVfrac = 1.0 - L_lkfrac
L_CVmax = L_CVn/L_CV0
L_CVn = L_n*L_CVfrac
L_lk0 = L_lkn/(exp(Dp_n*k_lk2)-1.0)
L_lkn = L_n*L_lkfrac
L_n = p_tot*f_n
NADHn = NADpool - NADn
NADn = NADpool/(1.0 + 1.0/NADNADHratn)
O2a = phi*pow(SaO2sup/(1.0-SaO2sup), 1.0/n_h)
O2c_init = O2c_n
O2c_n = phi*pow(ScO2_n/(1.0-ScO2_n), 1.0/n_h)
O2_init = O2_n
O2v_n = phi*pow(XOv_n/(Xtot_n - XOv_n), 1.0/n_h)
P_1n = (P_a2n+P_vn)/2.0
p1 = p_tot - p23
p3 = p23-p2
P_a2n = (G0n*P_an+Gn*P_vn)/(Gn+G0n)
PaO2 = O2a/henry_O2
pH_init = pH_mn
_Pn = _PCrn/PCrPn
p_totNADH = p_C1 + p_C3 + p23
P_vn = (Gtn/G_v)*(P_an-P_vs)+P_vs
r0 = (1.0-rfrac*rocc)
rfrac = 1.0-sqrt(sqrt(1.0-c_frac))
r_init = r_n
ScO2_n = (SaO2_n + SvO2_n)/2.0
sigma_en = sigma_e0*(exp(K_sigma*(r_n-r_0)/r_0) - 1.0) - sigma_coll
SvO2_n = XOv_n/Xtot_n
T_en = sigma_en*h_n
T_max0 = T_maxn/(1.0+k_aut*mu_n)
T_maxn = T_mn/exp(-pow(fabs((r_n-r_m)/(r_t-r_m)), n_m))
T_mn = (P_1n-P_icn)*r_n - T_en
u_psi = Dpsi_n/Dp_n
v_cn = Pa_CO2n
v_gluc_in = gluc_c / (k_gluc_trans+ gluc_c)
v_gluc_trans = CMRglucn/(v_gluc_in - _glucn/(_glucn+k_gluc_trans))
v_lac_in = lac_c/(k_lac_trans+lac_c)
v_lac_trans = (2.0*CMRglucn-CMRO2_n/3.0)/((_L0n*_Hyn)/((_L0n+k_lac_trans)*(_Hyn+k_lac_h))-v_lac_in)
Vmax_glucNn =  CMRglucn / (glyc_gn*glyc_pn*glyc_an*glyc_nn)
Vol_artn = Vol_totn/(1.0+VArat_n)
Vol_venn = Vol_totn*(VArat_n/(1.0+VArat_n))
v_on = O2c_n
v_pn = P_a2n
v_TCA = k_TCAn/(_Py0n*NADn)*(Km_NAD + NADn)*(Km_Py+_Py0n)
v_un = u_n
Xa_n = Xtot_n*(1.0-SaO2_n)
Xa = Xtot*(1.0-SaO2sup)
XOa_n = Xtot_n*SaO2_n
XOa = Xtot*SaO2sup
XOv_init = XOv_n
XOv_n = (CBFn*XOa_n-J_O2n)/CBFn
Xtot = Xtot_n
Xv_n = Xtot_n-XOv_n


# the following value is changed from TH's conversion
# to match the value in Tracy's paper
_ATPn := 2.2

_ADPn := 0.012
a_frac_n := 0.67
blood_frac := 0.0325
c3 := 0.11
CBFn := 0.008
CBFscale := 5000.0
C_buffi := 0.022
C_buffic := 0.025
CCO_offset := 0.0
c_frac := 0.8
C_im := 0.00675
ck1 := 0.01
ck2 := 0.02
CMRglucn := 0.0044
CMRO2_n := 0.02
C_v := 0.047
CVinh := 1.0
cytox_tot_tis := 0.0022
DHbdiff_offset := 0.0
DHbO2_offset := 0.0
DHbT_offset := 0.0
DHHb_offset := 0.0
Dp3_corr := -25.0
#Dp_CV0 := 90.0
dpH := 0.001
Dpsi_n := 145.0
E_a30 := 350.0

# changed to match Tracy's paper
E_c0 := 247.0

E_N0 := -320.0
Fdy := 96.4853

# changed to match Tracy's paper
G0rat := 5.0

gluc_c := 5.3
_glucn := 1.2
Gp0 := -30500.0
G_VAratn := 4.0
h_0 := 0.003
henry_O2 := 1.4000e-03
Inh_glucN := 3.0
k30 := 2.5e+05
k_ADPATP := 1055.0
k_aut := 1.0
Keq_MAshut := 10.0
k_gluc_trans := 6.2

# um...
k_lac_h := 0

k_lac_trans := 2.0
k_lk2 := 0.038
km := 0.025
Km_glucNAf := 0.2
Km_glucNgl := 0.5000e-01
Km_glucNN := 0.0
Km_glucNPf := 0.2
Km_NADf := 0.6
Km_Pyf := 0.005
k_nADPATP := 379.0
k_PCrATP_eq_ef := 166.0
k_PCrATP_ht := 0.00002
k_PytoLac_ht := 10.0
K_sigma := 10.0
k_unc := 1.0
_L0n := 3.0
lac_c := 1.0
L_CV0 := 0.4
L_lkfrac := 0.25
mu_max := 1.0
mu_min := -1.0
mu_n := 0.0
na := 4.33
NADcytn := 359.0
NADHcytn :=  50.0
NADNADHratn := 9.0
NADpool := 3.0
n_h := 2.5
n_m := 1.83
O2_n := 0.024
p23 := 4.0*2.0
p2 := 4.0*1.0
P_a := 50.0
Pa_CO2 := 40.0
Pa_CO2n := 40.0
P_an := 50.0
p_C1 := 8.0
p_C3 := 8.0-4.0
_PCrn := 2.6
PCrPn := 2.73
phi := 0.036
pH_mn := 7.4
pH_on := 7.0
P_ic := 4.5
P_icn := 4.5
p_tot := 18.40
P_vs := 1.5
_Py0n := 0.1
r_0 := 0.0126
R_autc := 2.2
R_auto := 1.5
R_autp := 4.0
R_autu := 0.0
r_CV := 5.0
r_m := 0.027
r_n := 0.0187
r_t := 0.018
SaO2_n := 0.96
SaO2sup := 0.96
sigma_coll := 62.79
sigma_e0 := 0.1425
t_c := 5.0
t_o := 20.0
t_p := 5.0
t_u := 0.5
u := 1.0
u_n := 1.0
VArat_n := 3.0
Vol_mit := 0.067
Vol_totn := 1.0
Xtot_n := 5.4
Z := 59.028
